# Definitions
left_parenthesis  = "("
right_parenthesis = ")"
plus              = "+"
minus             = "-"
multiply          = '*'
divide            = '/'
die_tag           = [dD]

integer = ("0" | [1-9] [0-9]*)

# Grammar
<roll_expression> ::= <arithmetic_expression>

<arithmetic_expression> ::= <addition_or_subtraction>

<addition_or_subtraction>            ::= <product_or_division> <addition_or_subtraction_right_hand>
<addition_or_subtraction_right_hand> ::= <plus_or_minus> <product_or_division> <addition_or_subtraction_right_hand>
                                       | &

<product_or_division>            ::= <signaled_value> <product_or_division_right_hand>
<product_or_division_right_hand> ::= <multiply_or_divide> <signaled_value> <product_or_division_right_hand>
                                   | &

<signaled_value> ::= <plus_or_minus> <value_expression>
                   | <value_expression>

<value_expression> ::= <dice_set_expression>

<dice_set_expression>            ::= <number_expression> <dice_set_expression_right_hand>
<dice_set_expression_right_hand> ::= <die_expression> <dice_set_expression_right_hand>
                                   | &

<number_expression> ::= left_parenthesis <arithmetic_expression> right_parenthesis
                      | <die_expression>
                      | integer

<die_expression> ::= die_tag <number_expression>

<plus_or_minus>      ::= plus
                       | minus
<multiply_or_divide> ::= multiply
                       | divide
