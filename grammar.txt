# Definitions
left_parenthesis  = "("
right_parenthesis = ")"
plus              = "+"
minus             = "-"
multiply          = '*'
divide            = '/'
die_tag           = [dD]

integer = ("0" | [1-9] [0-9]*)

# Grammar
<roll_expression> ::= <arithmetic>

<arithmetic> ::= <sum_or_sub>

<sum_or_sub>            ::= <prod_or_div> <sum_or_sub_right_hand>
<sum_or_sub_right_hand> ::= <plus_or_minus> <prod_or_div> <sum_or_sub_right_hand>
                          | &

<prod_or_div>            ::= <signaled_value> <prod_or_div_right_hand>
<prod_or_div_right_hand> ::= <mult_or_div> <signaled_value> <prod_or_div_right_hand>
                           | &

<signaled_value> ::= <plus_or_minus> <value>
                   | <value>

<value> ::= <dice_set>

<dice_set>            ::= <number> <dice_set_right_hand>
<dice_set_right_hand> ::= <die>
                        | &

<number> ::= left_parenthesis <arithmetic> right_parenthesis
           | <die>
           | integer

<die> ::= die_tag <number>

<plus_or_minus> ::= plus
                  | minus
<mult_or_div>   ::= multiply
                  | divide
